VARS     TITLE            '<< REXX - List all assigned variables       X
                                                      Documentation >>'
* Name:        VARLIST
*
* Purpose:     Determine the name of all initialized variables. List
*              the variable name and up to 80 bytes of value.
*
* Environment: REXX subroutine/function for any REXX environment.
*
* Features:
*
* Parameters:
* Notes:       Designed as a debugging aid.
*
*
         TITLE            '<< REXX - List all assigned variables       X
                                                     System Linkage >>'
VARLIST  ID    R12
         SPACE 2
         STM   R14,R12,12(R13)
         LR    R12,R15
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(,R13)
         LR    R13,R15
         XC    RC,RC
         B     MAINLINE
         SPACE 3
RC       DS    0F
         DS    AL1
RC1      DS    AL1
RC2      DS    AL1
RC3      DS    AL1
         SPACE 1
SAVEAREA DS    18F
         SPACE 3
RTNXT    DS    0H
         L     R13,SAVEAREA+4
         XC    8(4,R13),8(R13)
         XC    16(4,R13),16(R13)   R15 (return code) always
         LM    R14,R12,12(R13)        get set to zero.
         BR    R14
         TITLE            '<< REXX - List all assigned variables       X
                                                                    >>'
MAINLINE DS    0H
         USING ENVBLOCK,R8
         USING IRXEXTE,R9
         USING SHVBLOCK,R10
         LR    R8,R0               Address the environment block.
         L     R9,ENVBLOCK_IRXEXTE Address the REXX vector table.
         LA    R10,@SHV            Address the SHVBLOCK.
         MVI   SHVCODE,SHVNEXTV
         LA    R0,L'VARNAME
         ST    R0,SHVUSER
         LA    R0,VARNAME
         ST    R0,SHVNAMA
         LA    R0,VARVALUE
         ST    R0,SHVVALA
         LA    R0,L'VARVALUE
         ST    R0,SHVBUFL
         B     CALL_EXCOM
         TITLE            '<< REXX - List all assigned variables       X
                                            Fetch the next variable >>'
*
* Refer to the IRXEXCOM routine; pages 312-319 of SC28-1883-06 -
* TSO Extentions Version 2  REXX/MVS Reference
*
PLIST_EXCOM DC A(EXCOM_LIT,0,0)
         DC    A(@SHV+X'80000000')
         SPACE 1
@SHV     DS    0A
         DC    2A(0)
         DC    C'N'
         DS    XL3'000000'
         DC    5A(0)
         SPACE 1
EXCOM_LIT DC   CL8'IRXEXCOM'
         SPACE 2
CALL_EXCOM  DS  0H
         MVC   VARNAME,VARNAME-1
         MVC   VARVALUE,VARVALUE-1
         L     R15,IRXEXCOM        Get routine's address from vector.
         LA    R0,ENVBLOCK         Make sure REXX knows environment.
         LA    R1,PLIST_EXCOM
         BALR  R14,R15
         CLI   SHVRET,SHVLVAR      End of list?
         BE    RTNXT                  -yes-
         TITLE            '<< REXX - List all assigned variables       X
                                                     Display a line >>'
*
* Refer to the IRXSAY   routine; pages 336-339 of SC28-1883-06 -
* TSO Extentions Version 2  REXX/MVS Reference
*
         LA    R1,SAY_PLIST
         LA    R0,ENVBLOCK
         L     R15,IRXSAY
         BALR  R14,R15
         B     CALL_EXCOM
SAY_LIT        DC    CL8'WRITE   '
SAY_BUFF       DC    A(VARNAME)
SAY_LEN        DC    A(79)
SAY_PLIST      DC    A(SAY_LIT,SAY_BUFF,SAY_LEN+X'80000000')
         TITLE            '<< REXX - List all assigned variables       X
                                              Global Data Constants >>'
         SPACE 2
         DC    CL1' '
VARNAME  DS    CL20
         DC    CL2'= '
VARVALUE DS    CL57
         SPACE 2
         LTORG
         TITLE            '<< REXX - Get PDS directory entry           X
                                                 DSECTs and EQUates >>'
         YREGS
         SPACE 2
       IRXENVB
         SPACE 2
       IRXEXTE
         SPACE 2
       IRXSHVB
         END  VARLIST
